//Indicator - Volume Biased ADX
// By Karthik Marar - www.karthikmarar.blogspot.com
// Public release for personal use only.
//Please do not commercialize this indicator in its original or modified form.
//

_SECTION_BEGIN("Volume Biased ADX");
SetBarsRequired(200,0);

GraphXSpace = 5;
Vlp		= Param("Volume lookback period",30,20,300,10);
TP		= Param("Time Periods",14,1,50);
Vrg		= MA(V,Vlp);// average volume
Vf	 	= V / Vrg;// Volume Factor
adplus = abs(High-Ref(High,-1))*Vf;
adminus = abs(Ref(Low,-1)-Low) *Vf;
PlusDM = IIf(High>Ref(High,-1) AND Low>=Ref(Low,-1), adplus,
IIf(High>Ref(High,-1) AND Low<Ref(Low,-1)AND High-Ref(High,-1)>Ref(Low,-1)-Low,adplus, 0));
DIPlus= 100 * MA(PlusDM,TP) / ATR (TP);
MinusDM= IIf(Low<Ref(Low,-1) AND High<=Ref(High,-1), adminus,
IIf(High>Ref(High,-1) AND Low<Ref(Low,-1)AND High-Ref(High,-1)<Ref(Low,-1)-Low,adminus, 0));
DIMinus = 100 * MA(MinusDM,TP) / ATR (TP);
Diff= abs(DIPlus - DIMinus);
DISum= DIPlus + DIMinus;
ADXRaw= 100 * Wilders(Diff/DISum, TP);
Plot (ADXRaw,"VADX",ParamColor( "ADX color", colorLightBlue ),1|styleThick);
Plot (DiPlus,"DI+",ParamColor( "+DI color", colorGreen ),1|styleThick);
Plot (DiMinus,"DI-",ParamColor( "-DI color", colorRed),1|styleThick);
_SECTION_END(); 


